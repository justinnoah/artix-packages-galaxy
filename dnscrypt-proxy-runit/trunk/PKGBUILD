# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname="dnscrypt-proxy-runit"
pkgver=20180406
pkgrel=1
pkgdesc="runit service scripts for dnscrypt-proxy"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
depends=('dnscrypt-proxy')
groups=('runit-galaxy')
conflicts=('systemd-sysvcompat')
source=("dnscrypt-proxy.run::${_url}/dnscrypt-proxy/files/dnscrypt-proxy/run"
        "logdnscrypt-proxy.run::${_url}/dnscrypt-proxy/files/dnscrypt-proxy/log/run")
sha256sums=('6146f3adacfc9f680c64238a95532736817233b247f1b29be0101cef608549d8'
            '8f4a778fb697ca1287a2377a919a1fabfc3a324245decac8474f6c967561937d')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'dnscrypt-proxy'
    _inst_logsv 'dnscrypt-proxy'

    sed -e 's|dnscrypt_proxy|dnscrypt|g' \
        -i "${pkgdir}/etc/runit/sv/dnscrypt-proxy/log/run"

    sed -e 's|/etc/dnscrypt-proxy.toml|/etc/dnscrypt-proxy/dnscrypt-proxy.toml|g' \
        -i "${pkgdir}/etc/runit/sv/dnscrypt-proxy/run"
}
