# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-flasgger
pkgname=(python-flasgger python2-flasgger)
pkgver=0.9.0
pkgrel=2
pkgdesc="Extract swagger specs from your flask project"
url="https://github.com/rochacbruno/flasgger"
license=('PerlArtistic')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-flask' 'python2-flask' 'python-yaml'
             'python2-yaml' 'python-jsonschema' 'python2-jsonschema' 'python-mistune'
             'python2-mistune' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-flex' 'python2-flex'
              'python-marshmallow' 'python2-marshmallow' 'python-apispec' 'python2-apispec'
              'python-flask-restful' 'python2-flask-restful' 'python-flask-jwt' 'python2-flask-jwt'
              'python-decorator' 'python2-decorator')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/rochacbruno/flasgger/archive/$pkgver.tar.gz")
sha512sums=('d99b477a0c547539a03bd89456bc19b667c657d88a3bfa535de36167c415eefcfd6b560f6741b1b05093b34eac0e46729658b1221513399399e6714a22de28b6')

prepare() {
  cp -a flasgger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flasgger-$pkgver
  python setup.py build

  cd "$srcdir"/flasgger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flasgger-$pkgver
  PYTHONPATH="$PWD/etc/flasgger_package" python setup.py pytest

  cd "$srcdir"/flasgger-$pkgver-py2
  PYTHONPATH="$PWD/etc/flasgger_package" python2 setup.py pytest
}

package_python-flasgger() {
  depends=('python-flask' 'python-yaml' 'python-jsonschema' 'python-mistune' 'python-six')

  cd flasgger-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-flasgger() {
  depends=('python2-flask' 'python2-yaml' 'python2-jsonschema' 'python2-mistune' 'python2-six')

  cd flasgger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
