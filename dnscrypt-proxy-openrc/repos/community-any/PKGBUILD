# Maintainer: Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: artoo <artoo@artixlinux.org>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=dnscrypt-proxy-openrc
pkgver=20180330
pkgrel=1
pkgdesc="OpenRC dnscrypt-proxy init script"
arch=('any')
url="https://github.com/artix-linux/packages-galaxy"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'dnscrypt-proxy')
conflicts=('systemd-sysvcompat')
source=("dnscrypt-proxy.confd::${_url}/net-dns/dnscrypt-proxy/files/dnscrypt-proxy.confd-r2"
        "dnscrypt-proxy.initd::${_url}/net-dns/dnscrypt-proxy/files/dnscrypt-proxy.initd-r2")
sha256sums=('1373533ebac281a2cfc011253b02c4bbb8bf75fcb12e59d9d500d2ee7ebd0a88'
            '98dfb23e6db1384ba30527506ce535f4bcaec5ba10f4496e21fbdf63ca000901')

# pkgver() {
# 	date +%Y%m%d
# }

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package() {
    _inst_confd 'dnscrypt-proxy'
    _inst_initd 'dnscrypt-proxy'
}
