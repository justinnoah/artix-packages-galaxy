# Maintainer: artoo <artoo@artixlinux.org>

pkgname=calamares
pkgver=3.2.0
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/artix-linux/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=3.1.0'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache'
         'pythonqt' 'kiconthemes' 'kio' 'plasma-framework')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
optdepends=('calamares-branding: Artix branding')
source=("${url}/archive/v$pkgver.tar.gz"
        "artix-data.patch::https://github.com/artix-linux/calamares/commit/64bbc22ca6c358917ef1ef1cbc4f121494b935d0.patch"
        "servicescfg.patch::https://github.com/artix-linux/calamares/commit/2ad241a0717836ac406fff51ac7bfb14aaa2ff14.patch"
        "chrootcfg.patch::https://github.com/artix-linux/calamares/commit/c60e0394b6f5ae881a9b600724b3c9bd721e752f.patch"
        "postcfg.patch::https://github.com/artix-linux/calamares/commit/b6a8548b054d57f25fbee0f7e2d6afa9f1f1c48d.patch"
        "servicescfg-fix.patch::https://github.com/artix-linux/calamares/commit/f6c46654a3ff0699cdec79427d323924b53f0042.patch")
sha256sums=('c4587a4961a326b78b7b87ad8a716920b5de1aec51a33d538b2162e91c1cb8c4'
            '219aa401abf17405fd92b248c6e94f8cd854e271663c83f19cae1b1877faa59d'
            '29eeb51047a665ffd573c5a0b0e137e32c4d66b16f004e7cb3d4c13481c2ace3'
            'c273f96c3f8ba301adfcb7d64fdc1fbc8e0b187573aba5c0d6139793da185ebb'
            '2dd28e540d8b2ebdb434163abaf0e02ececf877a8396dcf0d45895d57f68d394'
            'c8a0b164538aa62bd7caedf83d26b076cd42e760e33422dda4dda9d7955ddc8a')

prepare() {
	cd $pkgname-$pkgver
	# patches here

	patch -Np 1 -i $srcdir/servicescfg.patch
	patch -Np 1 -i $srcdir/chrootcfg.patch
	patch -Np 1 -i $srcdir/postcfg.patch
	patch -Np 1 -i $srcdir/artix-data.patch
	patch -Np 1 -i $srcdir/servicescfg-fix.patch
}

build() {
	cd $pkgname-$pkgver

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt"
        make
}

package() {
	cd $pkgname-$pkgver/build
	make DESTDIR="$pkgdir" install

    cd ${srcdir}/$pkgname-$pkgver

    install -dm755 ${pkgdir}/etc/calamares/modules
    install -m644 data/artix/settings.conf ${pkgdir}/etc/calamares/settings.conf
    install -m644 data/artix/modules/{machineid,finished,displaymanager,welcome,umount,locale}.conf ${pkgdir}/etc/calamares/modules/
}
