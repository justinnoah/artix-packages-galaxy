# Maintainer: artoo <artoo@artixlinux.org>

pkgname=calamares
pkgver=3.2.0
pkgrel=3
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/artix-linux/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=3.1.0'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache'
         'pythonqt' 'kiconthemes' 'kio' 'plasma-framework')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
optdepends=('calamares-branding: Artix branding')
source=("${url}/archive/v$pkgver.tar.gz"
        0001-artix-data.patch
        0001-chrootcfg-module.patch
        0001-openrccfg-module.patch
        0001-postcfg-module.patch
        0001-runitcfg-module.patch)
sha256sums=('c4587a4961a326b78b7b87ad8a716920b5de1aec51a33d538b2162e91c1cb8c4'
            '16dea3c94ff92d6d6be65c2f22f2da03f900ddff651c776adbd40d93fcca0b34'
            '3555977f973eef124cbcd875cbdcac91b4ef9676347310cf18dba018d0c51c2c'
            'f72ea77b0c25a8839136a691b5fe0987efcead459d28a28a09976f69c470de2a'
            '5a96a6631b7b31e991fc037a23b3aa95c7951dbd3eef25fc4d0ccce4aebed8f8'
            'cb4c79d9dd5393a43d38245332b7c677783d41985b3b75020bf80ded7f573bfa')

prepare() {
	cd $pkgname-$pkgver
	# patches here

	patch -Np 1 -i $srcdir/0001-artix-data.patch
	patch -Np 1 -i $srcdir/0001-chrootcfg-module.patch
	patch -Np 1 -i $srcdir/0001-openrccfg-module.patch
	patch -Np 1 -i $srcdir/0001-postcfg-module.patch
	patch -Np 1 -i $srcdir/0001-runitcfg-module.patch
}

build() {
	cd $pkgname-$pkgver

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt"
        make
}

package() {
	cd $pkgname-$pkgver/build
	make DESTDIR="$pkgdir" install

    cd ${srcdir}/$pkgname-$pkgver

    install -dm755 ${pkgdir}/etc/calamares/modules
    install -m644 data/artix/settings.conf ${pkgdir}/etc/calamares/settings.conf
    install -m644 data/artix/modules/*.conf ${pkgdir}/etc/calamares/modules/
}
