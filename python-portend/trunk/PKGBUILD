# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgbase='python-portend'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-portend' 'python2-portend')
pkgver=2.3
pkgrel=2
pkgdesc="Use portend to monitor TCP ports for bound or unbound states."
arch=('any')
url="https://github.com/jaraco/portend"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-tox' 'python2-tox' 'python-tempora'
  'python2-tempora')
source=("${_pkgbase}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/ab/8f/70f5babd4a6a7c6a471e834c53be3ee3c89a6b3b16d6a85b2d25f2dc2671/${_pkgbase}-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('eb438c9967bd1540534bc9deb5bdb8b7ed41c7328aeee0c9376b00018ec6c07773c0d782efe3f460cf6e68bd4d899bceedf0c581dd29080cd3fe44a3a17b0004'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

prepare() {
    cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py build

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py build
}

check() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    tox

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    tox2
}

package_python-portend() {
    depends=('python' 'python-tempora')
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-portend() {
    depends=('python2' 'python2-tempora')
    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
