# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
_name=pytest-rerunfailures
pkgbase=python-pytest-rerunfailures
pkgname=('python-pytest-rerunfailures' 'python2-pytest-rerunfailures')
pkgver=4.1
pkgrel=2
pkgdesc="A py.test plugin that re-runs failed tests up to -n times to eliminate flakey failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0948242d26ebbac3c3252c755e8963cc0b958d8e1f6232b9f76310cefc7bea0844522df3620000c56c6ff7afdd3db2dd35488c8df4bede16d7a55117207c6911')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

package_python-pytest-rerunfailures() {
  depends=('python-pytest')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}

package_python2-pytest-rerunfailures() {
  depends=('python2-pytest')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}

# vim:set ts=2 sw=2 et:
