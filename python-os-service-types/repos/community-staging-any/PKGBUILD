# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-os-service-types
pkgname=(python-os-service-types python2-os-service-types)
pkgver=1.2.0
pkgrel=3
pkgdesc="Python library for consuming OpenStack sevice-types-authority data"
arch=('any')
url="https://pypi.python.org/pypi/os-service-types/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock'
              'python-testrepository' 'python2-testrepository')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-service-types/archive/$pkgver.tar.gz")
sha512sums=('f7e0aedfcf18514bb8a112a27363971361dfa4abeb05ec32f206197f07bbc55c119cf6b47058977925e77306658a83aab7619aa3a97f3581976e81ae5abef056')

prepare() {
  echo "global-include *.json" >> os-service-types-$pkgver/MANIFEST.in

  cp -a os-service-types-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/os-service-types-$pkgver
  python setup.py build

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-service-types-$pkgver
  python setup.py testr

  cd "$srcdir"/os-service-types-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-os-service-types() {
  depends=('python-pbr')

  cd "$srcdir"/os-service-types-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-os-service-types() {
  depends=('python2-pbr')

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
