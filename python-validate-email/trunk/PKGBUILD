# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-validate-email
pkgname=(python-validate-email python2-validate-email)
pkgver=1.3
pkgrel=2
pkgdesc="Validate_email verify if an email address is valid and really exists."
url="https://github.com/syrusakbary/validate_email"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/v/validate_email/validate_email-$pkgver.tar.gz")
sha512sums=('77abb9797c59e9aee20368814373191dac3749938d31402ec56d447a118849b391f7291642d1b028d32fc19bad465c5e6a3785af6651d6d64728db884f75a0c9')

prepare() {
  cp -a validate_email-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/validate_email-$pkgver
  python setup.py build

  cd "$srcdir"/validate_email-$pkgver-py2
  python2 setup.py build
}

package_python-validate-email() {
  depends=('python')

  cd validate_email-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-validate-email() {
  depends=('python2')

  cd validate_email-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
