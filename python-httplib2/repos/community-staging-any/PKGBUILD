# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgbase=python-httplib2
pkgname=(python-httplib2 python2-httplib2)
pkgver=0.11.3
pkgrel=2
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='https://github.com/httplib2/httplib2'
license=('MIT')
arch=('any')
makedepends=('ca-certificates' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python2-mock' 'python-pytest-cov' 'python2-pytest-cov'
              'python-pytest-runner' 'python2-pytest-runner' 'python-pytest-timeout'
              'python2-pytest-timeout')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/httplib2/httplib2/archive/v$pkgver.tar.gz"
        'cert.patch')
sha512sums=('d9e216901f1bdab9a43caa5d25ba5d39e851d391ebe257ee8be517964ce185aed2accab8a1caf93fc3ed018586445ab78c4e84f26ca647e0ab76810c2f06e741'
            'e260b86b222a65dffed151c5d2ee9ff16fd9d829e5932a5fa1cbb64b4e0195d70e4704733922091004f07d6d83ebe532aec07bc563b932f8e543dbce518b0714')

prepare() {
  cd httplib2-$pkgver
  patch -p1 -i "$srcdir"/cert.patch

  cp -a "$srcdir"/httplib2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py build

  cd "$srcdir"/httplib2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py pytest --addopts tests || warning "Tests failed"

  cd "$srcdir"/httplib2-$pkgver-py2
  python2 setup.py pytest --addopts tests || warning "Tests failed"
}

package_python-httplib2() {
  depends=('python' 'ca-certificates')

  cd httplib2-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-httplib2() {
  depends=('python2' 'ca-certificates')

  cd httplib2-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
