# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=deprecation
pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=2.0.5
pkgrel=2
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="http://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-unittest2')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('d42271860c50032023e559f9096156b694c414710e1122b377128885be923753bec51f308fab80d5dceaf324ad98863146e5dab4b3d8e0847234b5b2c235ad4b'
            'SKIP')
validpgpkeys=('E62866A3562575D95B570AF58EB71A70831B56D1') # Brian Curtin (brian.curtin) <brian@python.org>

prepare() {
  cp -a deprecation-$pkgver{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation-$pkgver
  python -m unittest discover

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
