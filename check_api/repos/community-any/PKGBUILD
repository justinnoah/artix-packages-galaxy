# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=check_api
pkgver=1.1.4
pkgrel=1
pkgdesc='A gate-keeper tool for the APIs.guru API collection'
arch=('any')
url='https://github.com/Mermade/check_api'
license=('BSD')
depends=('nodejs' 'semver' 'typescript')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c0f79c14f698911a88b94153cac721e89bc54eeb3341ebaa30460bcc59be664e542fdb7bc91a3f7123887b7078f44affb36dadfa5a6f58619d9f5c98332b4f40')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  rm -r "$pkgdir"/usr/lib/node_modules/check_api/node_modules/typescript \
        "$pkgdir"/usr/lib/node_modules/check_api/node_modules/.bin/ts* \
        "$pkgdir"/usr/lib/node_modules/check_api/node_modules/{,.bin/}semver

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/$pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
