# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-aspectlib
pkgname=('python-aspectlib' 'python2-aspectlib')
pkgver=1.4.2
pkgrel=3
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://github.com/ionelmc/python-aspectlib"
makedepends=('python-setuptools' 'python2-setuptools' 'python-fields' 'python2-fields')
checkdepends=('python-mock' 'python2-mock' 'python-tornado' 'python2-tornado'
              'python-process-tests' 'python2-process-tests' 'python2-trollius'
              'python-profilestats' 'python2-profilestats' 'python-pytest' 'python2-pytest') # 'mysql-python')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-aspectlib/archive/v$pkgver.tar.gz")
sha512sums=('fadb90b625ce2faac8f6d29ee28efaee09b4338c3c516c79d9e8753b7fb2ff45e0033174c5c5302f47f1f9e332824f37d56667fb05e66f85b808e8aa3a086f57')

prepare() {
  cp -a python-aspectlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-aspectlib-$pkgver
  python setup.py build

  cd "$srcdir"/python-aspectlib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-aspectlib-$pkgver
  LC_CTYPE=en_US.UTF-8 py.test || warning "https://github.com/ionelmc/python-aspectlib/issues/11"

  cd "$srcdir"/python-aspectlib-$pkgver-py2
  py.test2 || warning "Tests failed"
}

package_python-aspectlib() {
  depends=('python-fields')

  cd python-aspectlib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-aspectlib() {
  depends=('python2-fields')

  cd python-aspectlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
