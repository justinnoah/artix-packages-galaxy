# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname='openntpd-runit'
pkgver=20180314
pkgrel=2
pkgdesc="Runit service script for openntpd"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
depends=('openntpd')
groups=('runit-galaxy')
provides=('openntpd-runscripts')
replaces=('openntpd-runscripts')
conflicts=('systemd-sysvcompat' 'ntp' 'ntp-runit')
source=("openntpd.run::${_url}/openntpd/files/openntpd/run"
        "logopenntpd.run::${_url}/openntpd/files/openntpd/log/run")
sha256sums=('769e088fcffc08bec605fb9c4e192c395884c24ff98614f383334e22d6fbdfaf'
            '09925663d47d21d3040c371f70bdd559d004b10158c877b97adfc15c0ad766a4')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'openntpd'
    _inst_logsv 'openntpd'
}
