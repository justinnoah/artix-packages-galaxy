# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgbase=python-memcached
pkgname=(python2-memcached python-memcached)
pkgver=1.59
pkgrel=2
url='https://github.com/linsomniac/python-memcached'
arch=('any')
license=('PSF')
makedepends=('python2-setuptools' 'git' 'python2-six' 'python-six' 'python-setuptools')
source=("git://github.com/linsomniac/python-memcached#tag=$pkgver")
md5sums=('SKIP')

package_python2-memcached() {
  pkgdesc='Python2 interface to memcached'
  depends=('python2-six')

  cd 'python-memcached'

  python2 setup.py install --root=$pkgdir --optimize=1
}

package_python-memcached() {
  pkgdesc='Python interface to memcached'
  depends=('python-six')

  cd 'python-memcached'

  python3 setup.py install --root=$pkgdir --optimize=1
}

check() {
  cd 'python-memcached'

  python2 setup.py check
  python3 setup.py check
}

# getver: raw.githubusercontent.com/linsomniac/python-memcached/master/ChangeLog
# vim:set ts=2 sw=2 et:
