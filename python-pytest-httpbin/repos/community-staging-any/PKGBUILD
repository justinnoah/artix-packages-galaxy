# $Id: PKGBUILD 145298 2015-10-30 10:35:09Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-httpbin
pkgname=('python-pytest-httpbin' 'python2-pytest-httpbin')
pkgver=0.3.0
pkgrel=3
pkgdesc='A py.test fixture for httpbining code'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/pytest-httpbin'
makedepends=('python-pytest' 'python2-pytest' 'python-six' 'python2-six' 'httpbin' 'python2-httpbin')
checkdepends=('python-requests' 'python2-requests' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kevin1024/pytest-httpbin/archive/v$pkgver.tar.gz"
        "https://github.com/kevin1024/pytest-httpbin/pull/50/commits/65068a2632e51701d8dc97fc2e6d29086749b79a.patch"
        LICENSE)
sha512sums=('fdfd6ac9350151544852908f9a165ad4cde9135ffc86bdda8b2ecae21d3850ae20720b3fa150086e9501ac6138def5c63e05c7caf73be82c866220fab82de89a'
            'cbc6a0135704372e948e16040679337cedc34f388189f2c1652ef94b74012eb5f1a0d94ae91f0cf4a1a9a4156f46b0956707cd5f33aca102bcd88b2d01207930'
            '949cb93164ce92e0a5acc184cca2da5580e31def2d153771ebfa386030fa2da65bcd48670780096d2cefe3e631dc755fc3024ae417f2d1bb3333bd85a39baf22')

prepare() {
  # https://github.com/kevin1024/pytest-httpbin/issues/49
  patch -p1 -d pytest-httpbin-$pkgver < 65068a2632e51701d8dc97fc2e6d29086749b79a.patch
  cp -a pytest-httpbin-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-httpbin-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-httpbin-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-httpbin-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" python setup.py pytest

  cd "$srcdir"/pytest-httpbin-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" python2 setup.py pytest
}

package_python-pytest-httpbin() {
  depends=('python-pytest' 'python-six' 'httpbin')

  cd pytest-httpbin-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-httpbin() {
  depends=('python2-pytest' 'python2-six' 'python2-httpbin')

  cd pytest-httpbin-$pkgver-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
