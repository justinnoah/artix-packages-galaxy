# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-json-logger
pkgname=('python-json-logger' 'python2-json-logger')
pkgver=0.1.9
pkgrel=2
pkgdesc='A python library adding a json log formatter'
arch=('any')
license=('BSD')
url='https://github.com/madzak/python-json-logger'
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-v$pkgver.tar.gz::https://github.com/madzak/python-json-logger/archive/v$pkgver.tar.gz")
sha512sums=('5b9038fb05236ac23c5fbfd8f14550c192dc0900d89fb1fdeb0563b15d62f58bd17375758e45d2302696e097852b3e7dd24f509bf97e2582add2bf0269b278d6')

prepare() {
  cp -a python-json-logger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py build

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py test

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py test
}

package_python-json-logger() {
  depends=('python-setuptools')

  cd python-json-logger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-json-logger() {
  depends=('python2-setuptools')

  cd python-json-logger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
