# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=6.3.3
pkgrel=2
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python2-backports.functools_lru_cache'
  'python2-backports.unittest_mock' 'tree')
source=("${pkgbase}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/a3/fe/60797128186577348abc612fdd1011e737d7f01137c0c927dba489360fc3/cheroot-6.3.3.tar.gz"
        "LICENSE"
        "cheroot-fix-setup-python3.patch")
sha512sums=('98d998776ec7f2424d99c0d0749e55de189302e6f03b9ab339123fddb896734f5f10b73c563cdaccf2cc36bcdedb4929ea208181970fabd99ab53d93831cfc10'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            '5ec0001c803682f142e3eb1ebcf10614b3d4f2e64dfea874a868b7702a79b429ae86aea5c7b90711d573d5cc25e3296c3b64dc55598d926c06de59d35d8f04bc')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/cheroot-fix-setup-python3.patch"
}

package_python-cheroot() {
  depends=('python-six')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six' 'python2-backports.functools_lru_cache'
           'python2-backports.unittest_mock')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/cheroot" "${pkgdir}/usr/bin/cheroot2"
}

# vim:set ts=2 sw=2 et:
