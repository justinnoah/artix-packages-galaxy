# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=virtualbox-modules-artix
pkgname=('virtualbox-host-modules-artix' 'virtualbox-guest-modules-artix')
pkgver=5.2.8
pkgrel=13
_linux_major=4
_linux_minor=16
arch=('x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=("linux>=${_linux_major}.${_linux_minor}"
             "linux<${_linux_major}.$((_linux_minor+1))"
             "linux-headers>=${_linux_major}.${_linux_minor}"
             "linux-headers<${_linux_major}.$((_linux_minor+1))"
             "virtualbox-host-dkms>=$pkgver"
             'git')

# We need to build a modified version of vboxsf for Linux 4.16. Once
# Linux 4.17 is out, we should remove virtualbox-guest-modules-arch.
# https://bugzilla.redhat.com/show_bug.cgi?id=1481630#c65
_vboxsf_commit=6a782003ad95a383c8b19b570a532271f090ad35
source=("git+https://github.com/jwrdegoede/vboxsf#commit=$_vboxsf_commit")
sha256sums=('SKIP')

_extramodules=extramodules-${_linux_major}.${_linux_minor}-ARTIX

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  cd vboxsf
  make BUILDDIR=/usr/lib/modules/$_kernver/build
}

package_virtualbox-host-modules-artix(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox host kernel modules for Artix Kernel'
  depends=("linux>=${_linux_major}.${_linux_minor}"
           "linux<${_linux_major}.$((_linux_minor+1))")
  replaces=('virtualbox-modules' 'virtualbox-host-modules')
  conflicts=('virtualbox-modules' 'virtualbox-host-modules'
			 'virtualbox-host-dkms')
  provides=('VIRTUALBOX-HOST-MODULES')

  cd "/var/lib/dkms/vboxhost/${pkgver}_OSE/$_kernver/$CARCH/module"
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

  # systemd module loading
  printf "vboxdrv\nvboxpci\nvboxnetadp\nvboxnetflt\n" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

package_virtualbox-guest-modules-artix(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox guest kernel modules for Artix Kernel'
  license=('GPL')
  depends=("linux>=${_linux_major}.${_linux_minor}"
           "linux<${_linux_major}.$((_linux_minor+1))")
  replaces=('virtualbox-archlinux-modules' 'virtualbox-guest-modules')
  conflicts=('virtualbox-archlinux-modules' 'virtualbox-guest-modules'
             'virtualbox-guest-dkms')
  provides=('VIRTUALBOX-GUEST-MODULES')

  install -Dm644 -t "$pkgdir/usr/lib/modules/$_extramodules" vboxsf/vboxsf.ko

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +
}

# vim:set ts=2 sw=2 et:
