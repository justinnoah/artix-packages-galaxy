# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname='vsftpd-runit'
pkgver=20180314
pkgrel=2
pkgdesc="Runit service script for vsftpd"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
depends=('vsftpd')
groups=('runit-galaxy')
provides=('vsftpd-runscripts')
replaces=('vsftpd-runscripts')
conflicts=('systemd-sysvcompat')
source=("vsftpd.run::${_url}/vsftpd/files/vsftpd/run"
        "vsftpd-ipv6.run::${_url}/vsftpd/files/vsftpd-ipv6/run")
sha256sums=('7eeafe0e92918c7595333d346ba1bece66a33364b843bbf8015ae607a22e47f2'
            '15fb0087aa68201b46cd80a94523e1912094b426e265e26b1f25373c38d2146d')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'vsftpd'
    _inst_sv 'vsftpd-ipv6'
}
