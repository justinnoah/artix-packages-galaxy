# Maintainer: Chris Cromer <cromer@artixlinux.org>

_udevver=220
_pkgname=eudev

pkgname=lib32-eudev
pkgdesc="The userspace dev tools (udev) forked by Gentoo (32-bit)"
pkgver=3.2.5
pkgrel=1
arch=('x86_64')
url="http://www.gentoo.org/proj/en/eudev/"
license=('GPL')
depends=('lib32-glib2' 'lib32-glibc')
makedepends=('lib32-gcc-libs' 'lib32-util-linux' 'gobject-introspection' 'gperf' 'python2' 'gtk-doc')
provides=("lib32-udev=${_udevver}")
conflicts=("lib32-systemd")
options=(!makeflags !libtool)
source=("$pkgname-$pkgver.tar.gz::https://github.com/gentoo/eudev/archive/v${pkgver}.tar.gz")
sha256sums=('870920011285b919fca5c60eaa5670312eeffe3883dd8cf564cd1179fc639336')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgname}-${pkgver}"
    ./autogen.sh
    ./configure \
	--prefix=/usr \
        --with-rootprefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib32 \
        --sbindir=/usr/bin \
        --enable-introspection \
	--disable-manpages

    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" -C src/libudev install
    make DESTDIR="${pkgdir}" -C src/shared uninstall
    rm -rf ${pkgdir}/usr/include
}
