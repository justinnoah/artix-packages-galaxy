# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=3.7.0
pkgrel=2
pkgdesc='Python module for using MongoDB'
arch=('x86_64')
license=('APACHE')
url='http://pypi.python.org/pypi/pymongo/'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-gevent' 'python2-monotonic' 'pifpaf')
checkdepends_x86_64=('mongodb')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('c958c4c9f18e025104fb48dd31cddf407777052bff325308bbae7046ad1fdff26c84974634c6328bd897adcce46b9439eace30318f104decd684120ff300ab94')

prepare() {
  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

build() {
  cd "$srcdir"/pymongo-$pkgver
  python setup.py build

  cd "$srcdir"/pymongo2-$pkgver
  python2 setup.py build
}

check_x86_64() {
  export DB_PORT=29000

  cd "$srcdir"/pymongo-$pkgver
  pifpaf run mongodb python setup.py test

  cd "$srcdir"/pymongo2-$pkgver
  pifpaf run mongodb python2 setup.py test
}

package_python2-pymongo() {
  depends=('python2')
  optdepends=('python2-monotonic: enable support for a monotonic clock')

  cd "pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

# vim:set ts=2 sw=2 et:
