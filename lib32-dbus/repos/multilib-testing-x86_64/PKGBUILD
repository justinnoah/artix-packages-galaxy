# Maintainer: Oscar Campos <damnwidget@artixlinux.org>
# Contributor: Chris Cromer <cromer@artixlinux.org>
# Contributor : Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-dbus
_pkgbasename=dbus
pkgver=1.12.4
pkgrel=1
pkgdesc="Freedesktop.org message bus system (32-bit)"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
provides=(lib32-libdbus)
conflicts=(lib32-libdbus)
replaces=(lib32-libdbus)
depends=(dbus)
makedepends=(lib32-gcc-libs lib32-glib2 lib32-expat git autoconf-archive lib32-libx11)
_commit=636963fd753b538ee5e55c531e2fe7651b233b67 # tags/dbus-1.12.4^0
source=("git+https://anongit.freedesktop.org/git/dbus/dbus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd dbus
  git describe --tags | sed 's/^dbus-//;s/-/+/g'
}

prepare() {
  cd dbus
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd dbus

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib/dbus-1.0 \
      --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-static \
      --disable-verbose-mode \
      --disable-asserts \
      --disable-checks \
      --without-systemdsystemunitdir \
      --disable-systemd \
      --disable-user-session
  make
}

check() {
  cd dbus
  make check
}

package() {
  cd dbus

  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
  rm -r "$pkgdir"/{etc,var}

  install -d "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}
