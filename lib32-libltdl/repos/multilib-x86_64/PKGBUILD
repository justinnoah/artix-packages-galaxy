# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=lib32-libltdl
pkgver=2.4.6+40+g6ca5e224
pkgrel=4
pkgdesc='A generic library support script (32-bit)'
arch=(x86_64)
url='http://www.gnu.org/software/libtool'
license=(GPL)
groups=(multilib-devel)
depends=(sh tar lib32-glibc libtool)
makedepends=(gcc-multilib git help2man)
provides=("lib32-libtool=$pkgver")
conflicts=('lib32-libtool')
replaces=('lib32-libtool')
_commit=6ca5e224bc7bcc114a9ba2cf5dcf0fbf0ec40c9f
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit)
md5sums=('SKIP')


pkgver() {
  cd libtool
  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd libtool
  ./bootstrap
}

build() {
  cd libtool
  export CC="gcc -m32" CXX="g++ -m32"
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32" --libdir=/usr/lib32
  make
}

check() {
  cd libtool
  make check || :
}

package() {
  cd libtool
  make DESTDIR="$pkgdir" install-libLTLIBRARIES
}
