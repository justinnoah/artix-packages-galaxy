# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname='libvirt-runit'
pkgver=20180314
pkgrel=1
pkgdesc="Runit service script for libvirt"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
depends=('libvirt')
groups=('runit-galaxy')
provides=('libvirt-runscripts')
replaces=('libvirt-runscripts')
conflicts=('systemd-sysvcompat')
source=("libvirtd.run::${_url}/libvirt/files/libvirtd/run"
        "loglibvirtd.run::${_url}/libvirt/files/libvirtd/log/run")
sha256sums=('df264bf678679bc83f2c387ec2a90eb4dba90511d2e161c78e624371b114c233'
            'd361bf15b6bb61cbe74cee03390a6801bf853b31529263ae7f9dc31cecadbbfb')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'libvirtd'
    _inst_logsv 'libvirtd'
}
