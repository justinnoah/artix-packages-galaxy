# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyftpdlib
pkgname=('python-pyftpdlib' 'python2-pyftpdlib')
pkgver=1.5.4
pkgrel=2
pkgdesc='Very fast asynchronous FTP server library'
arch=('any')
url='https://github.com/giampaolo/pyftpdlib/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pysendfile' 'python2-pysendfile')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pyopenssl' 'python2-pyopenssl' 'python-psutil' 'python2-psutil')
source=("https://pypi.io/packages/source/p/pyftpdlib/pyftpdlib-$pkgver.tar.gz")
sha512sums=('6eb308021dad28c1c51a88b366947a71b7bc1802571dc14cedf3a264bd0cb8504ef990d7e3110d10709f971d4d5b0001bee859b58fb4d02a1b0d46532acc9295')

prepare() {
  cp -a pyftpdlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py build

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py pytest

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py pytest
}

package_python-pyftpdlib() {
  depends=('python')
  optdepends=('python-pyopenssl: FTPS support'
              'python-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyftpdlib() {
  depends=('python2')
  optdepends=('python2-pyopenssl: FTPS support'
              'python2-pysendfile: Speed up file transfers'
              'python2-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/ftpbench{,2}
}
