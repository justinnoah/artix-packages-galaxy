# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-aniso8601
pkgname=(python-aniso8601 python2-aniso8601)
pkgver=3.0.2
pkgrel=2
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-dateutil' 'python2-dateutil')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('828d5c509fff869036b3a616d7f8591d09eae38d05d9143e9218a000edd739d39f9354171380e848beb9f8904a90c8814d45eff0506caab1842014506376225a')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
  cp -a aniso8601-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build

  cd "$srcdir"/aniso8601-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest

  cd "$srcdir"/aniso8601-$pkgver-py2
  python2 setup.py pytest
}

package_python-aniso8601() {
  depends=('python')

  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-aniso8601() {
  depends=('python2')

  cd aniso8601-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
