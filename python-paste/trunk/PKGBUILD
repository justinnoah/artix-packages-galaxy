# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase=python-paste
pkgname=(python-paste python2-paste)
pkgver=2.0.3
pkgrel=3
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose')
optdepends=('python2-flup: WSGI utilities'
            'python-openid: Support for OpenID')
source=("https://files.pythonhosted.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz"
        python3.7.patch)
md5sums=('1231e14eae62fa7ed76e9130b04bc61e'
         'eaec86c9ff3da59a784163d6252bb18f')

prepare() {
  rm Paste-${pkgver}/paste/httpserver.py.orig

  # https://bitbucket.org/ianb/paste/pull-requests/41
  patch -d Paste-${pkgver} -Np1 < python3.7.patch

  cp -a Paste-${pkgver}{,-py2}

  cd Paste-${pkgver}-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

build() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py build

  cd "$srcdir"/Paste-$pkgver-py2
  python2 setup.py build
}

check() {
  # Excluding 'test_paste_website' because pythonpaste.org
  # redirects to web.archive.org, and thus breaks the test

  cd "$srcdir"/Paste-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3 -e '.*test_paste_website'

  cd "$srcdir"/Paste-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2 -e '.*test_paste_website'
}

package_python-paste() {
  depends=('python-six')

  cd Paste-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-paste() {
  depends=('python2-six')

  cd Paste-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
