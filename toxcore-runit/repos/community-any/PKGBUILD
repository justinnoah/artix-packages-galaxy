# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname='toxcore-runit'
pkgver=20180314
pkgrel=2
pkgdesc="Runit service script for toxcore"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
depends=('toxcore')
groups=('runit-galaxy')
provides=('toxcore-runscripts')
replaces=('toxcore-runscripts')
conflicts=('systemd-sysvcompat')
source=("toxbootstrapd.run::${_url}/toxcore/files/toxbootstrapd/run")
sha256sums=('0a453a13383d6a57a94a7c221e376e2599f833ac6935752591aa04610ba2015b')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'toxbootstrapd'
    sed -e 's|toxbootstrapd|tox-bootstrapd|g' -i "$pkgdir/etc/runit/sv/toxbootstrapd/run"
}
