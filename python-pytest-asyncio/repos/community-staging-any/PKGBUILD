# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.8.0.20180710
_commit=d2dc63597d6597805c88f11a2e7a0f2f642ce7d6
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
checkdepends=('python-pytest-cov' 'python-async_generator')
source=("$pkgname-$_commit.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/$_commit.tar.gz")
sha512sums=('cfc1f9e30fde936efe6b963d26817a803a82587e5769ca35df10203be058f3df87073d44b9e7e10977cf51beb91be72e855ce85a5285b8747bf3e4459487a33f')

prepare() {
  mv pytest-asyncio-{$_commit,$pkgver}
}

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
