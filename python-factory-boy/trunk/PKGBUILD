# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-factory-boy
pkgname=(python-factory-boy python2-factory-boy)
pkgver=2.11.1
pkgrel=2
pkgdesc="A versatile test fixtures replacement based on thoughtbot's factory_bot for Ruby"
url="https://github.com/FactoryBoy/factory-boy"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-faker' 'python2-faker')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-flask' 'python2-flask'
              'python-django' 'python2-django' 'python-sqlalchemy' 'python2-sqlalchemy'
              'python-flask-sqlalchemy' 'python2-flask-sqlalchemy' 'python-mongoengine'
              'python2-mongoengine' 'python2-mock' 'mongodb' 'pifpaf')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/FactoryBoy/factory_boy/archive/$pkgver.tar.gz"
        python3.7.patch)
sha512sums=('f2a0856212bb47f29007a9ec52af129c63a0546f3d4eb733ce42eb285cad4eecabfd917badb18b723e2ee653c09422328413a3eb38b1cad53e4e538a7baffbc2'
            '937f3185c48a593b957745d4ba540427fcf6d8ee604f6198b5c7a07caa3d5cad5abbc516dcc5a795b634373612c0fcc31d806f635c405a77db8cf32afaa2cb98')

prepare() {
  (cd factory_boy-$pkgver; patch -p1 -i ../python3.7.patch)
  cp -a factory_boy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/factory_boy-$pkgver
  python setup.py build

  cd "$srcdir"/factory_boy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/factory_boy-$pkgver
  pifpaf run mongodb --port 27017 python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/factory_boy-$pkgver-py2
  pifpaf run mongodb --port 27017 python2 setup.py pytest || warning "Tests failed"
}

package_python-factory-boy() {
  depends=('python-faker')

  cd factory_boy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-factory-boy() {
  depends=('python2-faker')

  cd factory_boy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
