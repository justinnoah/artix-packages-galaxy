# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-unidecode
pkgname=('python-unidecode' 'python2-unidecode')
pkgver=1.0.22
pkgrel=2
pkgdesc='ASCII transliterations of Unicode text'
arch=('any')
url='https://github.com/avian2/unidecode'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-unidecode-${pkgver}.tar.gz::https://github.com/avian2/unidecode/archive/unidecode-${pkgver}.tar.gz")
sha256sums=('a650064d1fdd2bf52a57c8daab8ebe674097b335028c0aba6aecfebe18da47a9')

prepare() {
  cp -r {,python-}unidecode-unidecode-${pkgver}
   mv {,python2-}unidecode-unidecode-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-unidecode-unidecode-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-unidecode() {
  depends=('python')

  cd python-unidecode-unidecode-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-unidecode() {
  depends=('python2')

  cd python2-unidecode-unidecode-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  mv "${pkgdir}"/usr/bin/unidecode{,2}
}

# vim: ts=2 sw=2 et:
