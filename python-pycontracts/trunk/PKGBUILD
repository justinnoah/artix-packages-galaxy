# $Id: PKGBUILD 140810 2015-09-19 13:17:30Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pycontracts
pkgname=(python-pycontracts python2-pycontracts)
pkgver=1.8.3
pkgrel=2
pkgdesc='Declare constraints on function parameters and return values'
arch=('any')
url="https://andreacensi.github.io/contracts"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'python-decorator' 'python2-decorator' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/P/PyContracts/PyContracts-$pkgver.tar.gz")
sha512sums=('470c093caaa1289da6f9bf40a3ea4e7b4099a3702a0941e398aaf0507a70630b64499b4f718bbb404ae36433433d534df4d0089dda9a5576895f9c2aff5b82fb')

prepare() {
  cp -a PyContracts-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyContracts-$pkgver
  python setup.py build

  cd "$srcdir"/PyContracts-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/PyContracts-$pkgver
  python setup.py nosetests

  cd "$srcdir"/PyContracts-$pkgver-py2
  rm -r src/contracts-py3k-test
  python2 setup.py nosetests
}

package_python-pycontracts() {
  depends=('python-pyparsing' 'python-decorator' 'python-six')

  cd PyContracts-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pycontracts() {
  depends=('python2-pyparsing' 'python2-decorator' 'python2-six')

  cd PyContracts-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
