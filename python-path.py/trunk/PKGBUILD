# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-path.py
pkgname=('python-path.py' 'python2-path.py')
pkgver=11.0.1
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
makedepends=('python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-flake8' 'python2-pytest-flake8')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('28705ce0cb46f63191d25dfd2ad793a76019066076e0a3f6e2cbbd6191c3403cd5fe35c0781953984b959848b342301995d7a17334cfd779e4e70eb1d957fc34')

prepare() {
  cp -a path.py-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py build

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py pytest

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py pytest
}

package_python-path.py() {
  depends=('python')

  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-path.py() {
  depends=('python2')

  cd path.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
