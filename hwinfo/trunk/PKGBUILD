# Maintainer:  Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus, madeye, ranguvar and ninja_pt

pkgname=hwinfo
pkgver=21.38
pkgrel=1.1
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686')
url='https://github.com/openSUSE/hwinfo'
license=('GPL2')
depends=('libx86emu' 'bash' 'perl')
makedepends=('sysfsutils' 'flex' 'git')
options=('!emptydirs')
source=("https://github.com/openSUSE/$pkgname/archive/$pkgver.tar.gz"
        'custom_ioctl.patch')
sha256sums=('03877e41c09c40708ba03f48192b8e1cff9bad67d11044d6abcc2ce56f7d710e'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

prepare() {
  # Patching
  cd "$pkgname-$pkgver"
  patch -p0 -i ../custom_ioctl.patch
  echo 'touch changelog' > git2log
}

build() {
  make -C "$pkgname-$pkgver" -j1 \
    HWINFO_VERSION="$pkgver" \
    CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" \
    LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname-$pkgver" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

# getver: https://github.com/openSUSE/hwinfo/releases
# vim:set ts=2 sw=2 et:
