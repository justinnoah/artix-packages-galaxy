# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-munch
pkgname=(python-munch python2-munch)
pkgver=2.3.2
pkgrel=2
pkgdesc="A dot-accessible dictionary (a la JavaScript objects)"
url="https://github.com/Infinidat/munch"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-yaml' 'python2-yaml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Infinidat/munch/archive/$pkgver.tar.gz")
sha512sums=('0890165338e407c934e31fb3dd5b222788cd3bb1b38e4c4aa59931f1e8da79aba0f5d3cbf2eac3edad2e3e51add1d35b46cfffb2e96b68b7493f29feb6f99ac3')

prepare() {
  cp -a munch-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/munch-$pkgver
  python setup.py build

  cd "$srcdir"/munch-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/munch-$pkgver
  pytest

  cd "$srcdir"/munch-$pkgver-py2
  pytest2
}

package_python-munch() {
  depends=('python-six')

  cd munch-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-munch() {
  depends=('python2-six')

  cd munch-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
