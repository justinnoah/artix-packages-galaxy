# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=httpbin
pkgname=(httpbin python2-httpbin)
pkgver=0.7.0
pkgrel=2
pkgdesc="HTTP Request and Response Service"
arch=('any')
url="https://github.com/requests/httpbin"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-flask' 'python2-flask'
             'python-markupsafe' 'python2-markupsafe' 'python-itsdangerous' 'python2-itsdangerous'
             'python-six' 'python2-six' 'python-decorator' 'python2-decorator' 'python-brotlipy'
             'python2-brotlipy' 'python-raven' 'python2-raven' 'python-werkzeug'
             'python2-werkzeug' 'python-gevent' 'python2-gevent' 'python-flasgger'
             'python2-flasgger' 'python-blinker' 'python2-blinker')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/requests/httpbin/archive/v$pkgver.tar.gz")
sha512sums=('faec48a0a2ac8800293b10281966a28195884ad2f69f0f28f1b8c8e1a7bfd8933ebbc9b541c80cdc19e1031a8ba9383afe8e372b9044436cb307dd1e8eddaae7')

prepare() {
  cp -a httpbin-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/httpbin-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py build

  cd "$srcdir"/httpbin-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/httpbin-$pkgver
  python test_httpbin.py

  cd "$srcdir"/httpbin-$pkgver-py2
  python2 test_httpbin.py
}

package_httpbin() {
  depends=('python-flask' 'python-markupsafe' 'python-itsdangerous' 'python-six' 'python-decorator'
           'python-brotlipy' 'python-raven' 'python-werkzeug' 'python-gevent' 'python-flasgger'
           'python-blinker')

  cd httpbin-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-httpbin() {
  depends=('python2-flask' 'python2-markupsafe' 'python2-itsdangerous' 'python2-six'
           'python2-decorator' 'python2-brotlipy' 'python2-raven' 'python2-werkzeug'
           'python2-gevent' 'python2-flasgger' 'python2-blinker')

  cd httpbin-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
