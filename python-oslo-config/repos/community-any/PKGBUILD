# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
pkgver=6.2.2
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-netaddr' 'python2-netaddr'
             'python-stevedore' 'python2-stevedore' 'python-debtcollector' 'python2-debtcollector'
             'python-oslo-i18n' 'python2-oslo-i18n' 'python-rfc3986' 'python2-rfc3986'
             'python-yaml' 'python2-yaml' 'python2-enum34')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-sphinx' 'python2-sphinx')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('d81769dace43ef5b5a3057f8ee4f30d4d799fd29e16cdce75374359e3720fc52164907ddb8ac16abe6606fdc42cfdac0670884ca3b38dfa315a605a037721d4d')

prepare() {
  sed -i '/argparse/d' oslo.config-$pkgver/requirements.txt

  cp -a oslo.config-$pkgver{,-py2}
  find oslo.config-$pkgver-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.config-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.config-$pkgver-py2
  python2 setup.py build 
}

check() {
  cd "$srcdir"/oslo.config-$pkgver
  python setup.py testr ||:

  cd "$srcdir"/oslo.config-$pkgver-py2
  PYTHON=python2 python2 setup.py testr ||:
}

package_python-oslo-config() {
  depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
           'python-oslo-i18n' 'python-rfc3986' 'python-yaml')

  cd "$srcdir"/oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-config() {
  depends=('python2-six' 'python2-netaddr' 'python2-stevedore' 'python2-debtcollector'
           'python2-oslo-i18n' 'python2-rfc3986' 'python2-yaml' 'python2-enum34')

  cd "$srcdir"/oslo.config-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv "$pkgdir"/usr/bin/oslo-config-generator{,2}
}

# vim:set ts=2 sw=2 et:
