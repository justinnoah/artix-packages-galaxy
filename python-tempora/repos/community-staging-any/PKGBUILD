# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgbase='python-tempora'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-tempora' 'python2-tempora')
pkgver=1.13
pkgrel=2
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=('any')
url="https://github.com/jaraco/tempora"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-six' 'python2-six' 'python-pytz'
  'python2-pytz')
checkdepends=('python-tox' 'python2-tox' 'python2-backports.unittest_mock')
source=("${pkgbase}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/27/67/f2a19a71e9bc2d460d75a6745f8de868ccdf99e6a56e5ad8e89e22d80b0d/tempora-1.13.tar.gz"
        'LICENSE')
sha512sums=('98927024f07196a51b8d61017786cfc258db289ab9c98942cca2501074c1675bf1213770ad00db1d38829017bb9928cccde7fb344c170b2f485fc68505737485'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
    # setuptools wont find version from git tag

    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py build

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py build
}

check() {
    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

    cd "${srcdir}/${_pkgbase}-${pkgver}"
    tox

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    tox2
}

package_python-tempora() {
    depends=('python' 'python-six' 'python-pytz')
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-tempora() {
    depends=('python2' 'python2-six' 'python2-pytz')
    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py install --root="${pkgdir}" --optimize=1
    mv "${pkgdir}/usr/bin/calc-prorate" "${pkgdir}/usr/bin/calc-prorate2"

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
