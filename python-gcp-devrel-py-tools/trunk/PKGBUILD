# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-gcp-devrel-py-tools
pkgname=('python-gcp-devrel-py-tools' 'python2-gcp-devrel-py-tools')
pkgver=0.0.15
_commit=87422ba91814529848a2b8bf8be4294283a3e041
pkgrel=2
pkgdesc='Tools for Cloud Platform Python libraries and samples'
arch=('any')
license=('Apache')
url='https://github.com/GoogleCloudPlatform/python-repo-tools'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-retrying' 'python2-retrying' 'python-packaging' 'python2-packaging'
             'python-pylint' 'python2-pylint')
source=("$pkgbase-$_commit.tar.gz::https://github.com/GoogleCloudPlatform/python-repo-tools/archive/$_commit.tar.gz")
sha512sums=('6ed884b0325aa212c8545e33ddd763ea4fe4980f40ceab166a0d3f65913698ec9a31d7471f6907fb8e391e4063db20d59bb4c688adcb3d5e17fb04025cce6dd2')

prepare() {
  cp -a python-repo-tools-$_commit{,-py2}
}

build() {
  cd "$srcdir"/python-repo-tools-$_commit
  python setup.py build

  cd "$srcdir"/python-repo-tools-$_commit-py2
  python2 setup.py build
}

package_python-gcp-devrel-py-tools() {
  depends=('python-requests' 'python-retrying' 'python-setuptools' 'python-packaging'
           'python-pylint')

  cd python-repo-tools-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-gcp-devrel-py-tools() {
  depends=('python2-requests' 'python2-retrying' 'python2-setuptools' 'python2-packaging'
           'python2-pylint')

  cd python-repo-tools-$_commit-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/gcp-devrel-py-tools{,2}
}

# vim:set ts=2 sw=2 et:
