# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-relaxed
pkgname=('python-pytest-relaxed' 'python2-pytest-relaxed')
pkgver=1.1.2
_commit=9cd769ce3b2145b5d9adf3631f839982436bb24e
pkgrel=2
pkgdesc='Relaxed test discovery for pytest'
arch=('any')
license=('BSD')
url='https://github.com/bitprophet/pytest-relaxed'
makedepends=('python-pytest32' 'python2-pytest32' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six')
source=("$pkgbase-$_commit.tar.gz::https://github.com/bitprophet/pytest-relaxed/archive/$_commit.tar.gz")
sha512sums=('7f3dbd729a19862cd813a411a142bbb1d612749336353a94c77f3f74a45b8df3e6a3bf7fe1bc59e63673c0b254409b0d970b6e3045a87591a816a1b17e48ffe2')

prepare() {
  mv pytest-relaxed-{$_commit,$pkgver}
  cp -a pytest-relaxed-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-relaxed-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-relaxed-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-relaxed-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-relaxed-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-relaxed() {
  depends=('python-pytest32' 'python-decorator' 'python-six')

  cd pytest-relaxed-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-relaxed() {
  depends=('python2-pytest32' 'python2-decorator' 'python2-six')

  cd pytest-relaxed-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
