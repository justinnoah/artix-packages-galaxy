# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-text-unidecode
pkgname=(python-text-unidecode python2-text-unidecode)
pkgver=1.2
pkgrel=2
pkgdesc="The most basic Text::Unidecode port"
url="https://github.com/kmike/text-unidecode"
license=('PerlArtistic')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/t/text-unidecode/text-unidecode-$pkgver.tar.gz")
sha512sums=('1ba3309dfdf94a9293c2aae4f7fd53d74b3742e2432928124196a3db9b523edce5f71e55485135dec10362bf69580d9f83707c6b6ab0a8075523fdc1b640723e')

prepare() {
  cp -a text-unidecode-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/text-unidecode-$pkgver
  python setup.py build

  cd "$srcdir"/text-unidecode-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/text-unidecode-$pkgver
  python setup.py pytest

  cd "$srcdir"/text-unidecode-$pkgver-py2
  python2 setup.py pytest
}

package_python-text-unidecode() {
  depends=('python')

  cd text-unidecode-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-text-unidecode() {
  depends=('python2')

  cd text-unidecode-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
